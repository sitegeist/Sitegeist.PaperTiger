prototype(Sitegeist.PaperTiger:Form) < prototype(Neos.Neos:ContentComponent) {

    class = ''
    action = ${'#form_' + node.identifier}
    anchor = afx`<a id={'form_' + node.identifier}></a>`

    backendStyle = ${StaticResource.content('Sitegeist.PaperTiger', 'Public/Styles/Backend.css')}
    backendScript = ${StaticResource.content('Sitegeist.PaperTiger', 'Public/Scripts/Backend.js')}

    # we put class into the context to make in available insode the runtime form
    @context {
        _formClass = ${this.class}
        _formAction = ${this.action}
    }

    renderer = Neos.Fusion:Case {
        runtimeForm {
            condition = ${node.context.live}

            renderer = Neos.Fusion:Join {
                anchor = ${props.anchor}
                form = Neos.Fusion.Form:Runtime.RuntimeForm {
                    # since the runtime form is uncached we have to add values required inside to the form context
                    @cache.context {
                        formClass = '_formClass'
                        formAction = '_formAction'
                    }

                    namespace = ${q(node).property('_identifier')}
                    attributes.class = ${_formClass}
                    attributes.action = ${_formAction}

                    process = Neos.Fusion.Form:Runtime.SingleStepProcess {
                        content = Sitegeist.PaperTiger:FieldCollection.Renderer {
                            nodePath = 'fields'
                        }
                        schema = Sitegeist.PaperTiger:FieldCollection.Schema {
                            nodePath = 'fields'
                        }
                        footer >
                    }
                    action = Sitegeist.PaperTiger:ActionCollection.Definition {
                        nodePath = 'actions'
                    }
                }
            }
        }

        backend {
            condition = ${node.context.inBackend}
            renderer = afx`
                <style @if={props.backendStyle} data-slipstream>{props.backendStyle}</style>
                <script @if={props.backendScript} data-slipstream>{props.backendScript}</script>
                <div class="papertiger-form">
                    <div class="papertiger-form__header">
                        <span class="paper-tiger-form__header-number">1</span>
                        <span class="paper-tiger-form__header-title">{Translation.translate('form.formFields.header', 'Form fields', [], 'Main', 'Sitegeist.PaperTiger')}</span>
                    </div>
                    <form class={_formClass}>
                        <Sitegeist.PaperTiger:FieldCollection.Renderer nodePath="fields"/>
                    </form>
                    <div class="papertiger-form__header">
                        <span class="paper-tiger-form__header-number">2</span>
                        <span class="paper-tiger-form__header-title">{Translation.translate('form.followUpActions.header', 'Follow up actions', [], 'Main', 'Sitegeist.PaperTiger')}</span>
                    </div>
                    <Sitegeist.PaperTiger:ActionCollection.Fieldnames nodePath="fields"/>
                    <Sitegeist.PaperTiger:ActionCollection.Preview nodePath="actions"/>
                </div>
            `
        }
    }
}
