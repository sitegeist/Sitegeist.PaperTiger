prototype(Sitegeist.PaperTiger:Field.Hidden) < prototype(Neos.Neos:ContentComponent) {
    name = ${q(node).property('name')}
    value = ${q(node).property('value')}
    style = 'display:none; !important'
    class = null

    renderer = Neos.Fusion:Case {
        backend {
            condition = ${renderingMode.isEdit}
            renderer = afx`
                <Sitegeist.PaperTiger:FieldContainer
                    field.name={props.name}
                >
                    <label>Hidden</label> {props.name}:{props.value}
                </Sitegeist.PaperTiger:FieldContainer>
            `
        }

        default {
            condition = true
            renderer = afx`
                <Neos.Fusion.Form:FieldContainer
                    field.name={props.name}
                    attributes.style={props.style}
                    attributes.class={props.class}
                    attributes.autocomplete="off"
                    attributes.tabindex="-1"
                >
                    <Neos.Fusion.Form:Hidden field.value={props.value}/>
                </Neos.Fusion.Form:FieldContainer>
            `
        }
    }
}
