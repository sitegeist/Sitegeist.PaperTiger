prototype(Sitegeist.PaperTiger:Action.Email.Definition) < prototype(Neos.Fusion:Component) {
    subject = ${q(node).property('subject')}
    subject.@process.asTemplate = Sitegeist.PaperTiger:Action.DataTemplate
    format = ${q(node).property('format')}
    plaintext = ${q(node).property('plaintext')}
    plaintext.@process.asTemplate = Sitegeist.PaperTiger:Action.DataTemplate
    plaintext.@if.isPlaintextOrMultipart = ${this.format != 'html'}
    html = ${q(node).property('html')}
    html.@if.isHtmlOrMultipart = ${this.format != 'plaintext'}
    html.@process.asTemplate = Sitegeist.PaperTiger:Action.DataTemplate
    recipientAddress = ${q(node).property('recipientAddress')}
    recipientAddress.@process.asTemplate = Sitegeist.PaperTiger:Action.DataTemplate
    recipientName = ${q(node).property('recipientName')}
    recipientName.@process.asTemplate = Sitegeist.PaperTiger:Action.DataTemplate
    senderAddress = ${q(node).property('senderAddress')}
    senderAddress.@process.asTemplate = Sitegeist.PaperTiger:Action.DataTemplate
    senderName = ${q(node).property('senderName')}
    senderName.@process.asTemplate = Sitegeist.PaperTiger:Action.DataTemplate
    replyToAddress = ${q(node).property('replyToAddress')}
    replyToAddress.@process.asTemplate = Sitegeist.PaperTiger:Action.DataTemplate
    carbonCopyAddress = ${q(node).property('carbonCopyAddress')}
    carbonCopyAddress.@process.asTemplate = Sitegeist.PaperTiger:Action.DataTemplate
    blindCarbonCopyAddress = ${q(node).property('blindCarbonCopyAddress')}
    blindCarbonCopyAddress.@process.asTemplate = Sitegeist.PaperTiger:Action.DataTemplate
    attachUploads = ${q(node).property('attachUploads')}
    testMode = ${q(node).property('testMode')}

    renderer = Sitegeist.PaperTiger:Action {
        @if.minRequirementsAreMet = ${props.recipientAddress && props.senderAddress && props.subject && (props.plaintext || props.html)}
        type = 'Sitegeist.Neos.SymfonyMailer:SendMail'
        options {
            subject = ${props.subject}
            text = ${props.plaintext}
            html = ${props.html}
            recipientAddress = ${props.recipientAddress}
            recipientName = ${props.recipientName}
            senderAddress = ${props.senderAddress}
            senderName = ${props.senderName}
            replyToAddress = ${props.replyToAddress}
            carbonCopyAddress = ${props.carbonCopyAddress}
            blindCarbonCopyAddress = ${props.blindCarbonCopyAddress}
            attachments = Neos.Fusion:Map {
                @if.isEnabled = ${props.attachUploads}
                items = ${data}
                itemName = "item"
                itemRenderer = ${item}
                itemRenderer.@if.isUpload = ${Type.instance(item, '\\Neos\\Flow\\ResourceManagement\\PersistentResource') || Type.instance(item, '\\Psr\\Http\\Message\\UploadedFileInterface') || Type.instance(item, '\\Sitegeist\\FusionForm\\Upload\\Domain\\CachedUploadedFileCollection')}
                @process.filterEmpty = ${Array.filter(value)}
            }
            testMode = ${props.testMode}
        }
    }
}
