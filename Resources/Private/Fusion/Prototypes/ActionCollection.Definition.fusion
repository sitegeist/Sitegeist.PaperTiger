prototype(Sitegeist.PaperTiger:ActionCollection.Definition) < prototype(Neos.Fusion:Component) {
  nodePath = null

  renderer = Sitegeist.PaperTiger:ActionCollection.Definition.Internal {
    actionMap = Neos.Fusion:Map {
      items = ${q(node).children(props.nodePath).children('[instanceof Sitegeist.PaperTiger:Action]').get()}
      itemName = 'node'
      itemRenderer = Sitegeist.PaperTiger:ActionCollection.Definition.ContentCase
    }
  }
}

prototype(Sitegeist.PaperTiger:ActionCollection.Definition.Internal) {
  @class = '\\Sitegeist\\PaperTiger\\FusionObjects\\ActionCollectionImplementation'
}

prototype(Sitegeist.PaperTiger:ActionCollection.Definition.ContentCase) < prototype(Neos.Fusion:Case) {

  override {
    @position = 'before definition'
    condition = Neos.Fusion:CanRender {
      type = ${node.nodeType.name + '.Action.Override'}
    }
    type = ${node.nodeType.name + '.Action.Override'}
  }

  definition {
    @position = 'end'
    condition = true
    type = ${node.nodeType.name + '.Action'}
  }
}
